load("@bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@bazel_skylib//lib:selects.bzl", "selects")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@llvm-project//:vars.bzl", "LLVM_VERSION_MAJOR")
load("@llvm//3rd_party/llvm-project/21.x/libcxx:libcxx_headers.bzl", "LIBCXX_HEADERS_FILES_21")
load("@llvm//3rd_party/llvm-project/22.x/libcxx:libcxx_headers.bzl", "LIBCXX_HEADERS_FILES_22")
load("@llvm//toolchain/runtimes:cc_runtime_static_library.bzl", "cc_runtime_stage0_static_library")
load("@llvm//toolchain/runtimes:cc_runtime_shared_library.bzl", "cc_runtime_stage1_shared_library")

IS_LLVM_22_OR_NEWER = int(LLVM_VERSION_MAJOR) >= 22

# Should replicate libcxx/include/CMakeLists.txt
filegroup(
    name = "libcxx_21_headers_files",
    srcs = LIBCXX_HEADERS_FILES_21,
    visibility = ["//visibility:public"],
)

filegroup(
    name = "libcxx_22_headers_files",
    srcs = LIBCXX_HEADERS_FILES_22,
    visibility = ["//visibility:public"],
)

filegroup(
    name = "libcxx_headers_files",
    srcs = [":libcxx_22_headers_files"] if IS_LLVM_22_OR_NEWER else [":libcxx_21_headers_files"],
    visibility = ["//visibility:public"],
)

_CONFIG_SITE_MUSL_LINES = [
    "#define _LIBCPP_HAS_MUSL_LIBC 1",
]

_CONFIG_SITE_NO_MUSL_LINES = [
    "#define _LIBCPP_HAS_MUSL_LIBC 0",
]

_CONFIG_SITE_THREAD_API_WIN32_LINES = [
    "#define _LIBCPP_HAS_THREAD_API_PTHREAD 0",
    "#define _LIBCPP_HAS_THREAD_API_WIN32   1",
]

_CONFIG_SITE_THREAD_API_PTHREAD_LINES = [
    "#define _LIBCPP_HAS_THREAD_API_PTHREAD 1",
    "#define _LIBCPP_HAS_THREAD_API_WIN32   0",
]

_CONFIG_SITE_LIBCPP_VISIBILITY_LINES = [
    "#define _LIBCPP_DISABLE_VISIBILITY_ANNOTATIONS",
]

_CONFIG_SITE_GENERIC_LINES = [
    "#define _LIBCPP_ABI_VERSION             1",
    "#define _LIBCPP_ABI_NAMESPACE           __1",
    "#define _LIBCPP_ABI_FORCE_ITANIUM       0",
    "#define _LIBCPP_ABI_FORCE_MICROSOFT     0",
    "#define _LIBCPP_HAS_THREADS             1 // TODO: Handle no threads",
    "#define _LIBCPP_HAS_MONOTONIC_CLOCK     1",
    "#define _LIBCPP_HAS_TERMINAL            1",
    "#define _LIBCPP_HAS_THREAD_API_EXTERNAL 0",
    "#define _LIBCPP_HAS_THREAD_API_C11      0 // FIXME: Is this guarding dead code?",
    "// #define _LIBCPP_NO_VCRUNTIME",
    "// #define _LIBCPP_TYPEINFO_COMPARISON_IMPLEMENTATION @_LIBCPP_TYPEINFO_COMPARISON_IMPLEMENTATION@",
    "#define _LIBCPP_HAS_FILESYSTEM          1",
    "#define _LIBCPP_HAS_RANDOM_DEVICE       1",
    "#define _LIBCPP_HAS_LOCALIZATION        1",
    "#define _LIBCPP_HAS_UNICODE             1",
    "#define _LIBCPP_HAS_WIDE_CHARACTERS     1",
    "#define _LIBCPP_HAS_STD_MODULES         0",
    "#define _LIBCPP_HAS_TIME_ZONE_DATABASE  1",
    "#define _LIBCPP_HAS_VENDOR_AVAILABILITY_ANNOTATIONS 0",
    "#define _LIBCPP_INSTRUMENTED_WITH_ASAN  0",
    "",
    "#define _LIBCPP_PSTL_BACKEND_SERIAL",
    "// #define _LIBCPP_PSTL_BACKEND_STD_THREAD",
    "// #define _LIBCPP_PSTL_BACKEND_LIBDISPATCH",
    "",
    "# // Hardening.",
    "#define _LIBCPP_HARDENING_MODE_DEFAULT _LIBCPP_HARDENING_MODE_NONE",
    "#define _LIBCPP_ASSERTION_SEMANTIC_DEFAULT 2",
    "",
    "// \"_LIBCPP_HAS_NO_LIBRARY_ALIGNED_ALLOCATION\", # GLIBC < 2.16",
]

selects.config_setting_group(
    name = "windows_static",
    match_all = [
        "@platforms//os:windows",
        "@llvm//runtimes:linkmode_static",
    ],
)

write_file(
    name = "__config_site",
    out = "include/__config_site",
    content = _CONFIG_SITE_GENERIC_LINES +
              select({
                  ":windows_static": _CONFIG_SITE_LIBCPP_VISIBILITY_LINES,
                  "//conditions:default": [],
              }) +
              select({
                  "@llvm//constraints/libc:musl": _CONFIG_SITE_MUSL_LINES,
                  "//conditions:default": _CONFIG_SITE_NO_MUSL_LINES,
              }) +
              select({
                  "@platforms//os:windows": _CONFIG_SITE_THREAD_API_WIN32_LINES,
                  "//conditions:default": _CONFIG_SITE_THREAD_API_PTHREAD_LINES,
              }),
    visibility = ["//visibility:public"],
)

copy_file(
    name = "__assertion_handler",
    src = "vendor/llvm/default_assertion_handler.in",
    out = "include/__assertion_handler",
)

copy_to_directory(
    name = "libcxx_headers_include_search_directory",
    srcs = [
        ":__config_site",
        ":__assertion_handler",
    ] + [
        ":libcxx_headers_files",
    ],
    root_paths = [
        "include",
    ],
    visibility = ["//visibility:public"],
)

cc_library(
    name = "headers",
    includes = [
        "src",
        "include",
        "generated_headers",
    ],
    hdrs = [(
        ":libcxx_22_headers_files" if IS_LLVM_22_OR_NEWER else ":libcxx_21_headers_files"
    ),
        ":__config_site",
        ":__assertion_handler",
    ],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "textual_hdrs",
    srcs = glob([
        "src/**/*.h",
        "src/**/*.ipp",
    ]),
    visibility = ["//visibility:public"],
)

cc_library(
    name = "libcxx",
    local_defines = [
        "NDEBUG",
        "LIBC_NAMESPACE=__llvm_libc_common_utils",
        "_LIBCPP_BUILDING_LIBRARY",
        "_LIBCPP_REMOVE_TRANSITIVE_INCLUDES",
        "LIBCXX_BUILDING_LIBCXXABI",
        "_LIBCPP_HAS_NO_PRAGMA_SYSTEM_HEADER",
    ],
    copts = [
        "-faligned-allocation",
        "-std=c++23",

        "-fvisibility=hidden",
        "-fvisibility-inlines-hidden",

        # "-fno-exceptions", # if no exceptions
        # "-fno-rtti",       # if no rtti

        # Compiler agnostic warnings to disable
        "-Wno-unused-parameter",
        "-Wno-long-long",
        "-Wno-nullability-completeness",

        # Clang specific warnings to disable
        "-Wno-user-defined-literals",
        "-Wno-covered-switch-default",
        "-Wno-suggest-override",
    ] + select({
        "@platforms//os:windows": [
            "-Wno-pragma-pack",
            "-Wno-unused-value",
        ],
        "//conditions:default": [],
    }),
    includes = [
        "include",
        "src",
    ],
    hdrs = [(
        ":libcxx_22_headers_files" if IS_LLVM_22_OR_NEWER else ":libcxx_21_headers_files"
    ),
        ":__config_site",
        ":__assertion_handler",
    ],
    textual_hdrs = glob([
        "src/**/*.h",
        "src/**/*.ipp",
    ]) + [
        "@libcxxabi//:textual_hdrs",
    ],
    srcs = [
        # LIBCXX_SOURCES
        "src/algorithm.cpp",
        "src/any.cpp",
        "src/bind.cpp",
        "src/call_once.cpp",
        "src/charconv.cpp",
        "src/chrono.cpp",
        "src/error_category.cpp",
        "src/exception.cpp",
        "src/expected.cpp",
        "src/filesystem/filesystem_clock.cpp",
        "src/filesystem/filesystem_error.cpp",
        "src/filesystem/path_parser.h",
        "src/filesystem/path.cpp",
        "src/functional.cpp",
        "src/hash.cpp",
        "src/include/apple_availability.h",
        "src/include/atomic_support.h",
        "src/include/config_elast.h",
        "src/include/refstring.h",
        "src/include/ryu/common.h",
        "src/include/ryu/d2fixed.h",
        "src/include/ryu/d2fixed_full_table.h",
        "src/include/ryu/d2s.h",
        "src/include/ryu/d2s_full_table.h",
        "src/include/ryu/d2s_intrinsics.h",
        "src/include/ryu/digit_table.h",
        "src/include/ryu/f2s.h",
        "src/include/ryu/ryu.h",
        "src/include/to_chars_floating_point.h",
        "src/include/from_chars_floating_point.h",
        "src/memory.cpp",
        "src/memory_resource.cpp",
        "src/new_handler.cpp",
        "src/new_helpers.cpp",
        "src/optional.cpp",
        "src/print.cpp",
        "src/random_shuffle.cpp",
        "src/ryu/d2fixed.cpp",
        "src/ryu/d2s.cpp",
        "src/ryu/f2s.cpp",
        "src/stdexcept.cpp",
        "src/string.cpp",
        "src/support/runtime/exception_fallback.ipp",
        "src/support/runtime/exception_glibcxx.ipp",
        "src/support/runtime/exception_libcxxabi.ipp",
        "src/support/runtime/exception_libcxxrt.ipp",
        "src/support/runtime/exception_msvc.ipp",
        "src/support/runtime/exception_pointer_cxxabi.ipp",
        "src/support/runtime/exception_pointer_glibcxx.ipp",
        "src/support/runtime/exception_pointer_msvc.ipp",
        "src/support/runtime/exception_pointer_unimplemented.ipp",
        "src/support/runtime/stdexcept_default.ipp",
        "src/support/runtime/stdexcept_vcruntime.ipp",
        "src/system_error.cpp",
        "src/typeinfo.cpp",
        "src/valarray.cpp",
        "src/variant.cpp",
        "src/vector.cpp",
        "src/verbose_abort.cpp",
    ] + [
        # LIBCXX_ENABLE_THREADS
        "src/atomic.cpp",
        "src/barrier.cpp",
        "src/condition_variable_destructor.cpp",
        "src/condition_variable.cpp",
        "src/future.cpp",
        "src/mutex_destructor.cpp",
        "src/mutex.cpp",
        "src/shared_mutex.cpp",
        "src/thread.cpp",
    ] + [
        # LIBCXX_ENABLE_RANDOM_DEVICE
        "src/random.cpp",
    ] + select({
        "@platforms//os:windows": [
            "src/support/win32/compiler_rt_shims.cpp",
            "src/support/win32/locale_win32.cpp",
            "src/support/win32/support.cpp",
            "src/support/win32/thread_win32.cpp",
        ],
        "//conditions:default": [],
    }) + [
        # LIBCXX_ENABLE_LOCALIZATION
        "src/fstream.cpp",
        "src/include/sso_allocator.h",
        "src/ios.cpp",
        "src/ios.instantiations.cpp",
        "src/iostream.cpp",
        "src/locale.cpp",
        "src/ostream.cpp",
        "src/regex.cpp",
        "src/strstream.cpp",
    ] + [
        # LIBCXX_ENABLE_FILESYSTEM
        "src/filesystem/directory_entry.cpp",
        "src/filesystem/directory_iterator.cpp",
        "src/filesystem/file_descriptor.h",
        "src/filesystem/operations.cpp",
        "src/filesystem/posix_compat.h",
        "src/filesystem/time_utils.h",
        # NOT LIBCXX_USE_COMPILER_RT
        # omit int128_builtins.cpp because it provides __muloti4 which is already provided
        # by llvm compiler_rt.
        # "src/filesystem/int128_builtins.cpp",
    ] + [
        # LIBCXX_ENABLE_NEW_DELETE_DEFINITIONS
    ],
    implementation_deps = [
        # Order matter. Search path should have C++ headers before any lib C headers.
        "@libcxx//:headers",
        "@libcxxabi//:headers",
    ] + select({
        "@platforms//os:macos": [],
        "@platforms//os:windows": [],
        "@platforms//os:linux": [
            # TODO(cerisier): Provide only a subset of linux UAPI headers for musl.
            # https://github.com/cerisier/toolchains_llvm_bootstrapped/issues/146
            "@kernel_headers//:kernel_headers",
        ],
    }) + [
        # libcxx depends on a set of internal llvm-libc headers. (libc/shared)
        # See project hand-in-hand.
        # https://discourse.llvm.org/t/rfc-project-hand-in-hand-llvm-libc-libc-code-sharing/77701
        "@llvm//3rd_party/llvm-project/x.x/libc:libc_headers",
    ] + select({
        "@llvm//platforms/config:musl": [
            "@musl_libc//:musl_libc_headers",
        ],
        "@llvm//platforms/config:gnu": [
            "@glibc//:gnu_libc_headers",
        ],
        "@platforms//os:windows": [
            "@mingw//:mingw_headers",
        ],
        "@platforms//os:macos": [],
    }),
)

cc_runtime_stage0_static_library(
    name = "libcxx.static",
    deps = [
        ":libcxx",
    ],
    visibility = ["//visibility:public"],
)

cc_runtime_stage1_shared_library(
    name = "libcxx.shared",
    deps = [
        ":libcxx",
    ],
    user_link_flags = [
        "-Wl,-soname,libc++.so.1",
    ],
    shared_lib_name = "libc++.so.1",
    visibility = ["//visibility:public"],
)
