load("@rules_cc//cc/toolchains:args.bzl", "cc_args")
load("@rules_cc//cc/toolchains/impl:documented_api.bzl", "cc_args_list")
load("//toolchain/args:llvm_target_triple.bzl", "LLVM_TARGET_TRIPLE", "LLVM_LINK_TARGET_TRIPLE")

package(default_visibility = ["//visibility:public"])

# COMMON RESET FLAGS
# --no-default-config
# -fno-spell-checking

    # if (target.cpu.arch.isArm()) {
    #     try argv.append(if (target.cpu.arch.isThumb()) "-mthumb" else "-mno-thumb");
    # }

# -mcpu for arm assembly 

cc_args(
    name = "resource_dir",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
        # We may need it for other actions too?
    ],
    args = [
        "-resource-dir",
        "{resource_dir}",
    ],
    format = {
        "resource_dir": "//runtimes:resource_directory",
    },
    data = [
        "//runtimes:resource_directory",
    ],
)

cc_args(
    name = "llvm_compile_target_for_platform",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = [
        "-target",
    ] + LLVM_TARGET_TRIPLE,
)

cc_args(
    name = "llvm_link_target_for_platform",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-target",
    ] + LLVM_LINK_TARGET_TRIPLE,
)

cc_args(
    name = "cosmo_header_paths",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = [
        "-isystem",
        "{cosmo_headers}",
        "-isystem",
        "{cosmo_headers}/libc/isystem",
        "-isystem",
        "{cosmo_headers}/libc",
        "-isystem",
        "{cosmo_headers}/libc/nt",
        "-iquote",
        "{cosmo_headers}",
        "-iquote",
        "{cosmo_headers}/third_party/libcxx",
    ],
    format = {
        "cosmo_headers": "@cosmo_libc//:cosmo_headers_include_search_directory",
    },
    data = [
        "@cosmo_libc//:cosmo_headers_include_search_directory",
    ],
)

cc_args(
    name = "cosmo_normalize_include",
    actions = [
        "@rules_cc//cc/toolchains/actions:c_compile",
        "@rules_cc//cc/toolchains/actions:cpp_compile",
        "@rules_cc//cc/toolchains/actions:cpp_header_parsing",
        "@rules_cc//cc/toolchains/actions:cpp_module_compile",
        "@rules_cc//cc/toolchains/actions:cpp_module_codegen",
        "@rules_cc//cc/toolchains/actions:clif_match",
        "@rules_cc//cc/toolchains/actions:objcpp_compile",
    ],
    args = [
        #"-include",
        #"toolchain/args/cosmo_normalize_with_linux.h",
        "-include",
        "{normalize_inc}"
    ],
    format = {
        "normalize_inc": "@cosmo_libc//:normalize_inc",
    },
    data = [
        "cosmo_normalize_with_linux.h",
        "@cosmo_libc//:normalize_inc",
    ],
)

cc_args(
    name = "cosmo_linux_defines",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = [
        "-D_COSMO_SOURCE",
        "-D__linux__",
        "-Dlinux",
        "-D__linux",
        "-DLITTLE_ENDIAN=1234",
        "-DBIG_ENDIAN=4321",
        "-DBYTE_ORDER=LITTLE_ENDIAN",
        "-DHAVE_SBRK=0",
    ],
)

cc_args(
    name = "cosmo_cxx_standard",
    actions = [
        "@rules_cc//cc/toolchains/actions:cpp_compile",
        "@rules_cc//cc/toolchains/actions:cpp_header_parsing",
        "@rules_cc//cc/toolchains/actions:cpp_module_compile",
        "@rules_cc//cc/toolchains/actions:cpp_module_codegen",
        "@rules_cc//cc/toolchains/actions:clif_match",
        "@rules_cc//cc/toolchains/actions:objcpp_compile",
    ],
    args = [
        "-std=gnu++23",
    ],
)

cc_args(
    name = "cosmo_lax_vector_conversions",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = [
        "-flax-vector-conversions",
    ],
)

cc_args(
    name = "cosmo_disable_mmx",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = [
        "-DCOSMO_DISABLE_MMX",
    ],
)

cc_args(
    name = "cosmo_simd_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = select({
        "//platforms/config:cosmo_x86_64": [
            "-mmmx",
            "-msse",
            "-msse2",
            "-msse3",
            "-mssse3",
        ],
        "//platforms/config:cosmo_aarch64": [
            "-ffixed-x18",
            "-ffixed-x28",
            "-fsigned-char",
            "-march=armv8.6-a+fp16+bf16+dotprod+crypto",
        ],
        "//conditions:default": [],
    }),
)

cc_args(
    name = "cosmo_default_link_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-Wl,-no-as-needed",
    ],
)

cc_args(
    name = "cosmo_linker_script",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-Wl,-T,{ape_lds}",
        "-Wl,-z,common-page-size=4096",
        "-Wl,-z,max-page-size=16384",
        "-Wl,--gc-sections",
        "-Wl,-z,noexecstack",
        "-Wl,-z,norelro",
    ],
    format = {
        "ape_lds": "@cosmo_libc//:ape_linker_script",
    },
    data = [
        "@cosmo_libc//:ape_linker_script",
    ],
)

cc_args(
    name = "cosmo_linker_script_aarch64",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-Wl,-T,{ape_lds}",
        "-Wl,-z,common-page-size=16384",
        "-Wl,-z,max-page-size=16384",
        "-Wl,--gc-sections",
        "-Wl,-z,noexecstack",
        "-Wl,-z,norelro",
    ],
    format = {
        "ape_lds": "@cosmo_libc//:ape_aarch64_linker_script",
    },
    data = [
        "@cosmo_libc//:ape_aarch64_linker_script",
    ],
)

cc_args(
    name = "libcxx_headers_include_search_paths",
    actions = [
        # cpp_compile_actions - lto_backend
        # Omitted to avoid unused arg warning for -I paths.
        "@rules_cc//cc/toolchains/actions:linkstamp_compile",
        "@rules_cc//cc/toolchains/actions:cpp_compile",
        "@rules_cc//cc/toolchains/actions:cpp_header_parsing",
        "@rules_cc//cc/toolchains/actions:cpp_module_compile",
        "@rules_cc//cc/toolchains/actions:cpp_module_codegen",
        "@rules_cc//cc/toolchains/actions:clif_match",
        "@rules_cc//cc/toolchains/actions:objcpp_compile",
    ],
    args = [
        "-isystem",
        "{libcxx_headers_include_search_path}",
        "-isystem",
        "{libcxxabi_headers_include_search_path}",
    ],
    format = {
        "libcxx_headers_include_search_path": "//runtimes/libcxx:libcxx_headers_include_search_directory",
        "libcxxabi_headers_include_search_path": "//runtimes/libcxx:libcxxabi_headers_include_search_directory",
    },
    data = [
        "//runtimes/libcxx:libcxx_headers_include_search_directory",
        "//runtimes/libcxx:libcxxabi_headers_include_search_directory",
    ],
)

cc_args(
    name = "cosmo_libcxx_headers_include_search_paths",
    actions = [
        # cpp_compile_actions - lto_backend
        # Omitted to avoid unused arg warning for -I paths.
        "@rules_cc//cc/toolchains/actions:linkstamp_compile",
        "@rules_cc//cc/toolchains/actions:cpp_compile",
        "@rules_cc//cc/toolchains/actions:cpp_header_parsing",
        "@rules_cc//cc/toolchains/actions:cpp_module_compile",
        "@rules_cc//cc/toolchains/actions:cpp_module_codegen",
        "@rules_cc//cc/toolchains/actions:clif_match",
        "@rules_cc//cc/toolchains/actions:objcpp_compile",
    ],
    args = [
        "-isystem",
        "{cosmo_headers}/third_party/libcxx",
        "-isystem",
        "{cosmo_headers}/third_party/libcxxabi",
    ],
    format = {
        "cosmo_headers": "@cosmo_libc//:cosmo_headers_include_search_directory",
    },
    data = [
        "@cosmo_libc//:cosmo_headers_include_search_directory",
    ],
)

cc_args(
    name = "crt_search_directory",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
        "@rules_cc//cc/toolchains/actions:lto_index_for_executable",
        "@rules_cc//cc/toolchains/actions:lto_index_for_dynamic_library",
        "@rules_cc//cc/toolchains/actions:lto_index_for_nodeps_dynamic_library",
    ],
    # clang::driver::Toolchain::GetFilePath("crtn.o") looks for those using
    # prefix directories (-B).
    args = [
        "-B{crt_objects_directory}",
    ],
    format = {
        "crt_objects_directory": "//runtimes:crt_objects_directory",
    },
    data = [
        "//runtimes:crt_objects_directory",
    ],
)

cc_args(
    name = "static_link_executable",
    actions = [
        "@rules_cc//cc/toolchains/actions:cpp_link_executable",
        "@rules_cc//cc/toolchains/actions:lto_index_for_executable",
        "@rules_cc//cc/toolchains/actions:lto_index_for_dynamic_library",
        "@rules_cc//cc/toolchains/actions:lto_index_for_nodeps_dynamic_library",
    ],
    args = [
        "-static-pie",
    ],
)

cc_args(
    name = "force_linker_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-v",
        "-fuse-ld=lld",
    ],
)

cc_args(
    name = "no_absolute_paths_for_builtins",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = [
        # If the compiler sometimes rewrites paths in the .d files without symlinks
        # (ie when they're shorter), it confuses Bazel's logic for verifying all
        # #included header files are listed as inputs to the action.
        "-no-canonical-prefixes",
    ],
)

cc_args(
    name = "deterministic_compile_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = [
        # Make C++ compilation deterministic. Use linkstamping instead of these
        # compiler symbols.
        "-Wno-builtin-macro-redefined",
        "-D__DATE__=\"redacted\"",
        "-D__TIMESTAMP__=\"redacted\"",
        "-D__TIME__=\"redacted\"",
        "-ffile-compilation-dir=.",  # Avoid absolute paths in binaries
        "-flax-vector-conversions",
    ],
)

cc_args(
    name = "ubsan_compiler_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = [
        "-fno-omit-frame-pointer",
        "-fsanitize=undefined",
        "-fsanitize-ignorelist={ubsan_ignore}",
    ],
    format = {
        "ubsan_ignore": "//sanitizers:ubsan_ignore",
    },
    data = ["//sanitizers:ubsan_ignore"],
)

# Note that sanitizers must use `cc_unsanitized_library` on third-party deps
# if they end up depending on an exec-configure binary, as linking that binary can fail
# since it was compiled with sanitizer support but linked without it.
cc_args(
    name = "ubsan_linker_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "{ubsan_path}",
    ],
    format = {
        "ubsan_path": "@compiler-rt//:ubsan.static",
    },
    data = ["@compiler-rt//:ubsan.static"],
)

alias(
    name = "ubsan_enabled",
    actual = select({
        "//toolchain:stage1_bootstrapping": "//config/stage1:ubsan_enabled",
        "//conditions:default": "//config:ubsan_enabled",
    }),
    visibility = ["//toolchain:__subpackages__"],
)

cc_args_list(
    name = "ubsan_flags",
    args = select({
        ":ubsan_enabled": [
            ":ubsan_compiler_flags",
            ":ubsan_linker_flags",
        ],
        "//conditions:default": [],
    }),
)

cc_args(
    name = "hermetic_compile_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:cpp_compile_actions",
        "@rules_cc//cc/toolchains/actions:c_compile_actions",
        # -nostdlibinc triggers a warning for assemble (no preprocess)
        "@rules_cc//cc/toolchains/actions:preprocess_assemble",
        "@rules_cc//cc/toolchains/actions:objc_compile",
        "@rules_cc//cc/toolchains/actions:objcpp_compile",
    ],
    args = [
        # We want to disable everything except builtin headers since they are
        # provided as part of the compiler toolchain repository.
        "-nostdlibinc",
    ],
)


config_setting(
    name = "empty_sysroot",
    flag_values = {
        "//config:empty_sysroot": "True",
    },
)

# Passing a sysroot to /dev/null has the side effect of removing all default
# library search paths that clang passes like -L/usr/lib.
cc_args(
    name = "empty_sysroot_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = select({
        ":empty_sysroot": [
            "--sysroot=/dev/null",
        ],
        "//conditions:default": [],
    }),
)

cc_args(
    # We provide the libc++ and libunwind ourselves through static_runtime_libs
    # and dynamic_runtime_libs.
    name = "hermetic_link_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        # will prevent clang to choose which libunwind to link against
        "--unwindlib=none",
        # will prevent clang to choose which libc++ to link against
        "-nostdlib++",
    ],
)

cc_args(
    name = "default_libs",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-L{libcxx_library_search_path}",
        "-L{libunwind_library_search_path}",
    ],
    data = [
        "//runtimes/libcxx:libcxx_library_search_directory",
        "//runtimes/libunwind:libunwind_library_search_directory",
    ],
    format = {
        "libcxx_library_search_path": "//runtimes/libcxx:libcxx_library_search_directory",
        "libunwind_library_search_path": "//runtimes/libunwind:libunwind_library_search_directory",
    },
)

# This makes the clang driver use compiler-rt compliant filenames
# and paths when resolving compiler runtime libraries from the resource-dir.
cc_args(
    name = "rtlib",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = select({
        "//platforms/config:cosmo": [
            "-rtlib=compiler-rt",
        ],
        "//conditions:default": [
            "-rtlib=compiler-rt",
        ],
    }),
)

cc_args(
    name = "cosmo_disable_default_crt",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "-nostartfiles",
        "-nodefaultlibs",
    ],
)

cc_args(
    name = "cosmo_startfiles",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
        "{cosmo_crt}",
    ],
    format = {
        "cosmo_crt": "//runtimes/cosmo:cosmo_crt.object",
    },
    data = [
        "//runtimes/cosmo:cosmo_crt.object",
    ],
)
