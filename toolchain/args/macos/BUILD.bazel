load("@bazel_lib//:bzl_library.bzl", "bzl_library")
load("@rules_cc//cc/toolchains:args.bzl", "cc_args")
load(
    "//toolchain:linker_policy.bzl",
    "MACOS_AARCH64_MINIMUM_OS_VERSION",
    "MACOS_AARCH64_SYSROOT_LABEL",
    "cc_link_spec",
)
load(
    ":macos_minimum_os_flag.bzl",
    "MACOS_MINIMUM_OS_VERSIONS",
    "macos_minimum_os_flag",
)

#TODO(cerisier): Replace with official rules_cc cc_sysroot
# once https://github.com/bazelbuild/rules_cc/pull/607 is merged
load(":sysroot.bzl", "cc_sysroot")

package(default_visibility = ["//visibility:public"])

cc_sysroot(
    name = "macos_sdk_sysroot",
    allowlist_include_directories = [
        MACOS_AARCH64_SYSROOT_LABEL,
    ],
    data = [
        MACOS_AARCH64_SYSROOT_LABEL,
    ],
    sysroot = MACOS_AARCH64_SYSROOT_LABEL,
)

#TODO(cerisier): Fix with new cc_args BuildSettingInfo formatter once it is ready.
#                See https://github.com/cerisier/toolchains_llvm_bootstrapped/issues/302
macos_minimum_os_flag(
    name = "macos_minimum_os_flag",
)

[
    config_setting(
        name = "macos_minimum_os_{}".format(version.replace(".", "_")),
        flag_values = {
            ":macos_minimum_os_flag": version,
        },
    )
    for version in MACOS_MINIMUM_OS_VERSIONS
]

cc_args(
    name = "macos_minimum_os_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = select({
        ":macos_minimum_os_{}".format(version.replace(".", "_")): [
            "-mmacosx-version-min={}".format(version),
        ]
        for version in MACOS_MINIMUM_OS_VERSIONS
    } | {
        "//conditions:default": [
            # Keep current default behavior when the Apple minimum OS is not specified.
            "-mmacosx-version-min=%s" % MACOS_AARCH64_MINIMUM_OS_VERSION,
        ],
    }),
)

# TODO(cerisier): Extract those into proper semantic args list.
cc_args(
    name = "default_link_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = [
    ] + cc_link_spec("macos_default_link_flags").args,
    env = cc_link_spec("macos_default_link_flags").env,
)

cc_args(
    name = "default_libs",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    # -l: is not supported by lld64
    args = cc_link_spec("macos_default_libs").args,
    data = cc_link_spec("macos_default_libs").data,
    format = cc_link_spec("macos_default_libs").format,
)

bzl_library(
    name = "sysroot",
    srcs = ["sysroot.bzl"],
    deps = ["@rules_cc//cc/toolchains:toolchain_rules"],
)
