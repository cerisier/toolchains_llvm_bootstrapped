
load("@rules_cc//cc/toolchains/impl:documented_api.bzl", "cc_args_list")
load("@rules_cc//cc/toolchains:args.bzl", "cc_args")
load("//config:defs.bzl", "NVIDIA_COMPUTE_CAPABILITIES")
load(":cc_toolchain.bzl", "cc_toolchain")
load(":declare_toolchains.bzl", "declare_toolchains")

package(default_visibility = ["//visibility:public"])

[
    config_setting(
        name = "nvidia_compute_capability_{}".format(compute_capability),
        flag_values = {
            "//config:nvidia_compute_capability": compute_capability,
        },
        visibility = ["//visibility:private"],
    )
    for compute_capability in NVIDIA_COMPUTE_CAPABILITIES
]

cc_args(
    name = "fail_on_link_actions",
    actions = [
        "@rules_cc//cc/toolchains/actions:link_actions",
    ],
    args = select({
        "//conditions:default": [
            "--linking-is-not-supported-for-device-code",
        ],
    }),
)

cc_args(
    name = "cuda_gpu_arch_flag",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = select(
        {
            ":nvidia_compute_capability_{}".format(compute_capability): [
                "--offload-arch={}".format(compute_capability),
            ]
            for compute_capability in NVIDIA_COMPUTE_CAPABILITIES
        } | {
            # letting clang decide
            "//conditions:default": [],
        },
    ),
)

cc_args(
    name = "cuda_device_only_flag",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = [
        "--cuda-path={cuda_path}",
        "--offload-device-only",
    ],
    data = [
        "@cuda_sdk//:cuda_path",
    ],
    format = {
        "cuda_path": "@cuda_sdk//:cuda_path",
    },
)

cc_args(
    name = "default_compile_flags",
    actions = [
        "@rules_cc//cc/toolchains/actions:compile_actions",
    ],
    args = [
        "-x", "cuda",
        "-std=c++20",
    ],
)

cc_args_list(
    name = "toolchain_args",
    args = [
        "//toolchain/cuda:cuda_gpu_arch_flag",
        "//toolchain/cuda:cuda_device_only_flag",
        # "//toolchain/cuda:cuda_host_only_flag",
        "//toolchain/cuda:default_compile_flags",

        # Common default compile flags.
        # "//toolchain/args:llvm_target_for_platform",
        "//toolchain/args:no_absolute_paths_for_builtins",
        "//toolchain/args:deterministic_compile_flags",
        "//toolchain/args:module_map",
        "//toolchain:sysroot_flags",
        "//toolchain:hermetic_compile_flags",

        "//toolchain/args:libcxx_headers_include_search_paths",
    ] + [
            "//toolchain/args/linux:kernel_headers_include_search_paths",
            # "//toolchain/args/linux:musl_libc_headers_include_search_paths",
            "//toolchain/args/linux:glibc_headers_include_search_paths",

        # no link
        ":fail_on_link_actions",
    ],
)

declare_toolchains()
