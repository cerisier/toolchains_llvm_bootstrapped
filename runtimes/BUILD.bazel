load("@bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@bazel_skylib//rules:common_settings.bzl", "string_setting")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("//toolchain/args:llvm_target_triple.bzl", "LLVM_TARGET_TRIPLE")
load("//toolchain/runtimes:cc_stage0_object.bzl", "cc_stage0_object")
load(":copy_to_resource_directory.bzl", "copy_to_resource_directory")

#TODO(cerisier): duplicate this per runtime repo ? or do differently ?
string_setting(
    name = "linkmode",
    build_setting_default = "dynamic",
    values = [
        "static",
        "dynamic",
    ],
    visibility = ["//visibility:public"],
)

config_setting(
    name = "linkmode_static",
    flag_values = {
        ":linkmode": "static",
    },
    visibility = ["//visibility:public"],
)

config_setting(
    name = "linkmode_dynamic",
    flag_values = {
        ":linkmode": "dynamic",
    },
    visibility = ["//visibility:public"],
)

filegroup(
    name = "none",
    srcs = [],
    visibility = ["//toolchain:__subpackages__"],
)

filegroup(
    name = "static_runtime_lib",
    srcs = select({
        "@platforms//os:linux": [
            "@libcxx//:libcxx.static",
            "@libcxxabi//:libcxxabi.static",
            "@libunwind//:libunwind.static",
        ],
        "//platforms/config:abi_gnu": [
            "@libcxx//:libcxx.static",
            "@libcxxabi//:libcxxabi.static",
            "@libunwind//:libunwind.static",
        ],
        "//platforms/config:abi_msvc": [
            "@libcxx//:libcxx.static",
            "@libcxxabi//:libcxxabi.static",
            # TODO: check if we need libunwind support also for MSVC
        ],
        "//conditions:default": [],
    }),
    visibility = ["//toolchain:__subpackages__"],
)

filegroup(
    name = "dynamic_runtime_lib",
    srcs = select({
        "@platforms//os:linux": [
            "@libcxx//:libcxx.shared",
            "@libcxxabi//:libcxxabi.shared",
            "@libunwind//:libunwind.shared",
        ],
        # TODO(zbarsky): windows?
        "//conditions:default": [],
    }),
    visibility = ["//toolchain:__subpackages__"],
)

cc_library(
    name = "empty",
    srcs = ["empty.c"],
)

cc_stage0_object(
    name = "crti_object",
    srcs = [
        ":empty",
    ],
    out = "crti.o",
    copts = [
        "-target",
    ] + LLVM_TARGET_TRIPLE,
    visibility = ["//visibility:public"],
)

cc_stage0_object(
    name = "crtn_object",
    srcs = [
        ":empty",
    ],
    out = "crtn.o",
    copts = [
        "-target",
    ] + LLVM_TARGET_TRIPLE,
    visibility = ["//visibility:public"],
)

copy_to_directory(
    name = "crt_objects_directory_linux",
    srcs = select({
        "//platforms/config:musl": [
            # -static
            "//runtimes/musl:musl_crt1.object",
            # static-pie
            "//runtimes/musl:musl_rcrt1.object",
            "//runtimes/musl:musl_Scrt1.object",
        ],
        "//platforms/config:gnu": [
            "//runtimes/glibc:glibc_Scrt1.object",
            "//runtimes/glibc:glibc_crt1.object",
        ],
    }) + [
        ":crti_object",
        ":crtn_object",
        "@compiler-rt//:crtbeginS_object",
        "@compiler-rt//:crtbeginT_object",
        "@compiler-rt//:crtbegin_object",
        "@compiler-rt//:crtendS_object",
        "@compiler-rt//:crtend_object",
    ],
    include_external_repositories = ["**"],
    root_paths = [
        ".",
        "runtimes/musl",
    ],
    target_compatible_with = [
        "@platforms//os:linux",
    ],
    visibility = ["//visibility:public"],
)

copy_to_directory(
    name = "crt_objects_directory_windows_gnu",
    srcs = [
        "//runtimes/mingw:crt2.o",
        "//runtimes/mingw:crtbegin.o",
        "//runtimes/mingw:crtend.o",
        "//runtimes/mingw:dllcrt2.o",
    ],
    root_paths = [
        "runtimes/mingw",
    ],
    target_compatible_with = [
        "@platforms//os:windows",
        "//constraints/abi:gnu",
    ],
    visibility = ["//visibility:public"],
)

# MSVC doesn't need custom CRT objects - it uses the system CRT
filegroup(
    name = "crt_objects_directory_windows_msvc",
    srcs = [],
    visibility = ["//visibility:public"],
)

alias(
    name = "crt_objects_directory",
    actual = select({
        "@platforms//os:linux": ":crt_objects_directory_linux",
        "//platforms/config:abi_gnu": ":crt_objects_directory_windows_gnu",
        "//platforms/config:abi_msvc": ":crt_objects_directory_windows_msvc",
    }),
    visibility = ["//visibility:public"],
)

copy_to_resource_directory(
    name = "resource_directory",
    srcs = {
        "//runtimes/compiler-rt:clang_rt.builtins.static": "libclang_rt.builtins",
    } | select({
        "//config:ubsan_enabled": {
            "//runtimes/compiler-rt:clang_rt.ubsan.static": "libclang_rt.ubsan_standalone",
        },
        "//conditions:default": {},
    }),
    visibility = ["//visibility:public"],
)
