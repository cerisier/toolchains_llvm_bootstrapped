load("@bazel_lib//lib:copy_file.bzl", "copy_file")
load("@bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@bazel_skylib//rules:write_file.bzl", "write_file")

copy_file(
    name = "libcxx.static",
    src = "@libcxx//:libcxx.static",
    out = "libc++.a",
    visibility = ["//visibility:public"],
)

copy_file(
    name = "libcxx.shared",
    src = "@libcxx//:libcxx.shared",
    out = "libc++.so.1",
    visibility = ["//visibility:public"],
)

copy_file(
    name = "libcxxabi.static",
    src = "@libcxxabi//:libcxxabi.static",
    out = "libc++abi.a",
    visibility = ["//visibility:public"],
)

copy_file(
    name = "libcxxabi.shared",
    src = "@libcxxabi//:libcxxabi.shared",
    out = "libc++abi.so.1",
    visibility = ["//visibility:public"],
)

copy_file(
    name = "libcxxabi.shared.symlink",
    src = "@libcxxabi//:libcxxabi.shared",
    out = "libc++abi.so",
    allow_symlink = True,
    visibility = ["//visibility:public"],
)

write_file(
    name = "libcxx.version_script",
    out = "libc++.so",
    content = [
        "INPUT(",
        "libc++.so.1 libc++abi.so.1",
    ] + select({
        "//platforms/config:gnu": [
            # will be available under that name //runtimes/libgcc:libgcc_library_search_directory
            "libgcc_s.so.1",
        ],
        "//conditions:default": [
            # will be available under that name //runtimes/libunwind:libunwind_library_search_directory
            "libunwind.so.1",
        ],
    }) + [
        ")",
    ],
    visibility = ["//visibility:public"],
)

copy_to_directory(
    name = "libcxx_library_search_directory",
    srcs = [
        "libcxx.static",
        "libcxxabi.static",
    ] + select({
        "@platforms//os:windows": [],
        "//conditions:default": [
            "libcxx.version_script",
            "libcxx.shared",
            "libcxxabi.shared",
            "libcxxabi.shared.symlink",
        ],
    }),
    visibility = ["//visibility:public"],
)

alias(
    name = "libcxx_headers_include_search_directory",
    actual = "@libcxx//:libcxx_headers_include_search_directory",
    visibility = ["//visibility:public"],
)

alias(
    name = "libcxxabi_headers_include_search_directory",
    actual = "@libcxxabi//:libcxxabi_headers_include_search_directory",
    visibility = ["//visibility:public"],
)
