load("@bazel_lib//lib:copy_file.bzl", "copy_file")
load("@bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@rules_cc//cc:cc_library.bzl", "cc_library")
load("@toolchains_llvm_bootstrapped//toolchain/runtimes:cc_runtime_static_library.bzl", "cc_runtime_stage0_static_library")
load("@toolchains_llvm_bootstrapped//toolchain/runtimes:cc_stage0_object.bzl", "cc_stage0_object")
load("//runtimes:defs.bzl", "stub_library")
load("//toolchain/args:llvm_target_triple.bzl", "LLVM_TARGET_TRIPLE")

PICOLIBC_CFLAGS = [
    "-nostdlib",
    "-D_LIBC",
    "-ftls-model=local-exec",
    "-fno-common",
    "-fno-stack-protector",
    "-ffunction-sections",
    "-fdata-sections",
    "-fno-builtin",
] + select({
    "@platforms//cpu:x86_64": [
        "-frounding-math",
    ],
    "//conditions:default": [],
})

cc_library(
    name = "picolibc_libc",
    srcs = [
        "@picolibc//:compile_hdrs",
        "@picolibc//:compile_srcs",
    ],
    copts = PICOLIBC_CFLAGS,
    implementation_deps = [
        "@picolibc//:picolibc_internal_headers",
        "@picolibc//:libc_headers_for_picolibc",
    ],
    textual_hdrs = [
        "@picolibc//:textual_srcs",
    ],
    visibility = ["//visibility:public"],
)

cc_runtime_stage0_static_library(
    name = "picolibc_libc.static",
    visibility = ["//visibility:public"],
    deps = [":picolibc_libc"],
)

cc_library(
    name = "picolibc_crt1",
    srcs = select({
        "@platforms//cpu:x86_64": ["crt/crt1_x86_64.S"],
        "@platforms//cpu:aarch64": ["crt/crt1_aarch64.S"],
    }),
    visibility = ["//visibility:public"],
)

cc_stage0_object(
    name = "picolibc_crt1.object",
    srcs = [":picolibc_crt1"],
    out = "crt1.o",
    copts = [
        "-target",
    ] + LLVM_TARGET_TRIPLE,
    visibility = ["//visibility:public"],
)

copy_file(
    name = "picolibc_libc.a",
    src = ":picolibc_libc.static",
    out = "libc.a",
    allow_symlink = True,
    visibility = ["//visibility:public"],
)

PICOLIBC_LIBS = [
    "m",
    "rt",
    "pthread",
    "util",
    "resolv",
    "dl",
    "crypt",
    "xnet",
]

# Keep compatibility with tooling that still links these companion libs.
[
    stub_library(
        name = "picolibc_lib{}_stub".format(lib),
        out = "stubs/lib{}.a".format(lib),
        visibility = ["//visibility:public"],
    )
    for lib in PICOLIBC_LIBS
]

copy_to_directory(
    name = "picolibc_library_search_directory",
    srcs = [
        ":picolibc_libc.a",
    ] + [
        ":picolibc_lib{}_stub".format(lib)
        for lib in PICOLIBC_LIBS
    ],
    replace_prefixes = {
        "stubs": "",
    },
    visibility = ["//visibility:public"],
)

alias(
    name = "picolibc_headers_include_search_directory",
    actual = "@picolibc//:picolibc_headers_include_directory",
    visibility = ["//visibility:public"],
)
