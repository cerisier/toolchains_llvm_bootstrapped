load("@bazel_lib//lib:copy_file.bzl", "copy_file")
load("@bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("//toolchain:selects.bzl", "platform_llvm_binary")

copy_file(
    name = "libunwind",
    src = "@libunwind//:libunwind.static",
    out = "libunwind.a",
    allow_symlink = True,
)

alias(
    name = "llvm_ar",
    actual = platform_llvm_binary("bin/llvm-ar"),
)

[
    genrule(
        name = lib,
        srcs = [],
        outs = ["lib{}.a".format(lib)],
        tools = [":llvm_ar"],
        cmd = "$(location :llvm_ar) rc $@",
        visibility = ["//visibility:public"],
    ) for lib in ["gcc_s"]
]

config_setting(
    name = "stub_libgcc_s",
    flag_values = {
        "//config:experimental_stub_libgcc_s": "True",
    },
)

copy_to_directory(
    name = "libunwind_library_search_directory",
    srcs = [
        "libunwind.a",
    ] + select({
        ":stub_libgcc_s": [
            "libgcc_s.a",
        ],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
)
