load("@bazel_tools//tools/build_defs/repo:utils.bzl", "get_auth")
load("@bazel_lib//lib:repo_utils.bzl", "repo_utils")

def _write_sdk_settings(rctx):
    sdk_settings_json = rctx.attr.sdk_settings_json
    valid_deployment_targets_bzl = rctx.attr.valid_deployment_targets_bzl

    if not sdk_settings_json and not valid_deployment_targets_bzl:
        return

    if not sdk_settings_json or not valid_deployment_targets_bzl:
        fail("Both sdk_settings_json and valid_deployment_targets_bzl must be set together")

    sdk_settings = json.decode(rctx.read(sdk_settings_json), default = None)
    if sdk_settings == None:
        fail("Unable to decode '{}' as JSON".format(sdk_settings_json))

    supported_targets = sdk_settings.get("SupportedTargets", {})
    macosx = supported_targets.get("macosx", {})
    valid_deployment_targets = macosx.get("ValidDeploymentTargets", None)

    if valid_deployment_targets == None:
        fail("Missing SupportedTargets.macosx.ValidDeploymentTargets in '{}'".format(sdk_settings_json))
    if type(valid_deployment_targets) != "list":
        fail("Expected SupportedTargets.macosx.ValidDeploymentTargets to be a list in '{}'".format(sdk_settings_json))

    for target in valid_deployment_targets:
        if type(target) != "string":
            fail("Expected deployment target values in '{}' to be strings".format(sdk_settings_json))

    rctx.file(
        valid_deployment_targets_bzl,
        """# Generated by http_pkg_archive from {sdk_settings_json}. Do not edit.
MACOS_VALID_DEPLOYMENT_TARGETS = {valid_deployment_targets}
""".format(
            sdk_settings_json = sdk_settings_json,
            valid_deployment_targets = json.encode(valid_deployment_targets),
        ),
    )

def _http_pkg_archive_impl(rctx):
    rctx.download(
        url = rctx.attr.urls,
        output = ".downloaded.pkg",
        sha256 = rctx.attr.sha256,
        canonical_id = " ".join(rctx.attr.urls),
        auth = get_auth(rctx, rctx.attr.urls),
    )

    strip_prefix = ""
    if rctx.attr.strip_prefix:
        strip_prefix = rctx.attr.strip_prefix

    args = []

    for include in rctx.attr.includes:
        args.extend(["--include", strip_prefix + "/" + include])

    for exclude in rctx.attr.excludes:
        args.extend(["--exclude", strip_prefix + "/" + exclude])

    if strip_prefix:
        args.extend(["--strip-components", str(len(strip_prefix.split("/")))])

    args.extend(["--expand-full", ".downloaded.pkg", rctx.attr.dst])

    host_pkgutil = Label("@toolchain-extra-prebuilts-%s//:bin/pkgutil" % (repo_utils.platform(rctx).replace("_", "-")))
    res = rctx.execute([rctx.path(host_pkgutil)] + args)
    if res.return_code != 0:
        fail("Failed to extract package: {}".format(res.stderr))

    rctx.delete(".downloaded.pkg")

    for file, label in rctx.attr.files.items():
        rctx.file(file, rctx.read(label))

    _write_sdk_settings(rctx)

    return rctx.repo_metadata(reproducible = True)

http_pkg_archive = repository_rule(
    _http_pkg_archive_impl,
    attrs = {
        "urls": attr.string_list(mandatory = True),
        "sha256": attr.string(mandatory = True),
        "dst": attr.string(mandatory = True),
        "includes": attr.string_list(),
        "excludes": attr.string_list(),
        "strip_prefix": attr.string(),
        "files": attr.string_keyed_label_dict(),
        "sdk_settings_json": attr.string(),
        "valid_deployment_targets_bzl": attr.string(),
    },
)
